from pathlib import Path
from typing import Dict, Set

import pandas as pd
from snakemake.utils import min_version

# require minimum snakemake version
min_version("6.10.0")


# =====================================
# Workflow config items
configfile: "config/config.yaml"


samplesheet = pd.read_csv(config["samplesheet"], index_col="sample")
containers: Dict[str, str] = config["containers"]
# =====================================

# =====================================
# Constants for workflow
GB = 1_024
rules_dir = Path("workflow/rules").resolve()
fast5_dir = Path(config["fast5_dir"]).resolve()
data_dir = Path("data").resolve()
GUPPY_VERSION = containers["guppy-gpu"].split(":")[-1]
rule_log_dir = Path("logs/stderr")
# =====================================


# =====================================
# Generate the set of required output files from the pipeline
output: Set[Path] = set()
for sample, row in samplesheet.iterrows():
    output.add(data_dir / f"fastqs/guppy_v{GUPPY_VERSION}/{sample}.fq.gz")
# =====================================


rule all:
    input:
        output,


include: rules_dir / "basecall.smk"
